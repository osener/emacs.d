#+TITLE: Ozan Sener's Emacs configuration
#+OPTIONS: num:nil ^:nil
* Load some lisp
#+BEGIN_SRC emacs-lisp
  (require 'cl)
#+END_SRC

Install some additional packages
#+BEGIN_SRC emacs-lisp
  (require-package 'jade-mode)
  (require-package 'dash)
  (require-package 'multiple-cursors)
  (require-package 'async)
  (require-package 'restclient)
  (require-package 'typo)
  (require-package 'dic-lookup-w3m)
  (require-package 'protobuf-mode)
#+END_SRC

* Programming languages
** Scheme
#+BEGIN_SRC emacs-lisp
  (add-hook 'scheme-mode-hook 'sanityinc/lisp-setup)
#+END_SRC
*** Chicken Scheme
Load chicken
#+BEGIN_SRC emacs-lisp
  (add-to-list 'load-path "/usr/local/lib/chicken/6/")
  (autoload 'chicken-slime "chicken-slime" "SWANK backend for Chicken" t)
  (add-hook 'scheme-mode-hook
            (lambda ()
              (slime-mode t)))
#+END_SRC

** Clojure
#+BEGIN_SRC emacs-lisp
  (require-package 'clj-refactor)

  (add-hook 'clojure-mode-hook
            (lambda ()
              (clj-refactor-mode 1)
              (cljr-add-keybindings-with-prefix "C-c C-m")))
#+END_SRC
*** Testing & static analysis
Static checking using kibit
#+BEGIN_SRC emacs-lisp
  (require 'compile)
  (add-to-list 'compilation-error-regexp-alist-alist
               '(kibit "At \\([^:]+\\):\\([[:digit:]]+\\):" 1 2 nil 0))
  (add-to-list 'compilation-error-regexp-alist 'kibit)

  ;; A convenient command to run "lein kibit" in the project to which
  ;; the current emacs buffer belongs to.
  (defun kibit ()
    "Run kibit on the current project.
  Display the results in a hyperlinked *compilation* buffer."
    (interactive)
    (compile "lein kibit"))
#+END_SRC

*** Nrepl
Display colors in nrepl buffers
#+BEGIN_SRC emacs-lisp
  (defun nrepl-emit-output (buffer string &optional bol)
    "Using BUFFER, emit STRING.
    If BOL is non-nil, emit at the beginning of the line."
    (with-current-buffer buffer
      (nrepl-emit-output-at-pos buffer string nrepl-input-start-mark bol)
      (ansi-color-apply-on-region (marker-position nrepl-output-start) (point-max))))
#+END_SRC

Specify the print length to be 100 to stop infinite sequences killing things.
#+BEGIN_SRC emacs-lisp
  (defun live-nrepl-set-print-length ()
    (cider-send-string-sync "(set! *print-length* 100)" "clojure.core"))

  (add-hook 'cider-connected-hook 'live-nrepl-set-print-length)
#+END_SRC

Use persistent REPL history
#+BEGIN_SRC emacs-lisp
  (setq cider-history-file (expand-file-name ".cider-history"
                                             user-emacs-directory))
#+END_SRC

*** Datomic
Load snippets for datomic
#+BEGIN_SRC emacs-lisp
  (require-package 'datomic-snippets)
  (require 'datomic-snippets)
#+END_SRC

*** Cosmetic
Highlight expression on eval
#+BEGIN_SRC emacs-lisp
  (require-package 'highlight)
  (require-package 'nrepl-eval-sexp-fu)
  (require 'nrepl-eval-sexp-fu)
  (setq nrepl-eval-sexp-fu-flash-duration 0.3)
  (set-face-attribute 'nrepl-eval-sexp-fu-flash nil :background "#103050")
#+END_SRC

Fancy literals (from emacs-live)
#+BEGIN_SRC emacs-lisp
(eval-after-load 'clojure-mode
  '(font-lock-add-keywords
    'clojure-mode `(("(\\(fn\\)[\[[:space:]]"
                     (0 (progn (compose-region (match-beginning 1)
                                               (match-end 1) "λ")
                               nil))))))

(eval-after-load 'clojure-mode
  '(font-lock-add-keywords
    'clojure-mode `(("\\(#\\)("
                     (0 (progn (compose-region (match-beginning 1)
                                               (match-end 1) "ƒ")
                               nil))))))

(eval-after-load 'clojure-mode
  '(font-lock-add-keywords
    'clojure-mode `(("\\(#\\){"
                     (0 (progn (compose-region (match-beginning 1)
                                               (match-end 1) "∈")
                               nil))))))
#+END_SRC

Highlight sexp
#+BEGIN_SRC emacs-lisp
  (dolist (hook (mapcar #'derived-mode-hook-name sanityinc/lispy-modes))
    (add-hook hook 'hl-sexp-mode))
#+END_SRC

** C/C++
Auto indentation (C-j is harder to reach on Colemak)
#+BEGIN_SRC emacs-lisp
  (add-hook 'c-mode-common-hook '(lambda ()
        (local-set-key (kbd "RET") 'newline-and-indent)))
#+END_SRC

Style
#+BEGIN_SRC emacs-lisp
  (setq-default
   c-default-style "bsd"
   tab-width 2
   c-indent-level 2
   c-basic-offset 2)
#+END_SRC
** Javascript
#+BEGIN_SRC emacs-lisp
  (setq js2-concat-multiline-strings 'eol
        js2-rebind-eol-bol-keys t
        js2-idle-timer-delay 0.1
        js2-strict-inconsistent-return-warning nil)
#+END_SRC

*** Declare globals
#+BEGIN_SRC emacs-lisp
  (setq-default js2-global-externs '("module" "require" "jQuery"
                                     "$" "_" "buster" "assert"
                                     "setTimeout" "clearTimeout"
                                     "setInterval" "clearInterval"
                                     "__dirname" "console" "JSON"
                                     "cengiz"))


  ;; After js2 has parsed a js file, we look for jslint globals decl comment
  ;; ("/* global Fred, _, Harry */") and add any symbols to a buffer-local var of
  ;; acceptable global vars Note that we also support the "symbol: true" way of
  ;; specifying names via a hack (remove any ":true" to make it look like a plain
  ;; decl, and any ':false' are left behind so they'll effectively be ignored as
  ;; you can't have a symbol called "someName:false"
  (add-hook 'js2-post-parse-callbacks
            (lambda ()
              (when (> (buffer-size) 0)
                (let ((btext (replace-regexp-in-string
                              ": *true" " "
                              (replace-regexp-in-string "[\n\t ]+" " " (buffer-substring-no-properties 1 (buffer-size)) t t))))
                  (mapc (apply-partially 'add-to-list 'js2-additional-externs)
                        (split-string
                         (if (string-match "/\\* *global *\\(.*?\\) *\\*/" btext) (match-string-no-properties 1 btext) "")
                         " *, *" t))
                  ))))
#+END_SRC
*** Insert semicolons automatically
#+BEGIN_SRC emacs-lisp
  (require 'js2-mode)
  (defvar js2-semicolon-contexts
    (list js2-NAME js2-LP js2-SCRIPT js2-CALL js2-BLOCK))
  (defun sp-js2-maybe-insert-semicolon (id action context)
    (when (and (eq action 'insert)
               (save-excursion
                 (goto-char (- (point) 1))
                 (skip-chars-backward " \t")
                 (memq (js2-node-type (js2-node-at-point (point)))
                       js2-semicolon-contexts)))
      (save-excursion
        (let ((forward-sexp-function nil))
          (goto-char (- (point) 1))
          (forward-sexp))
        (if (looking-at-p "[^[:graph:]]*$")
            (insert ";")))))

  (sp-local-pair 'js2-mode "(" nil :post-handlers
                 '(:add sp-js2-maybe-insert-semicolon))
#+END_SRC

*** json-mode
#+BEGIN_SRC emacs-lisp
  (require-package 'json-mode)
  (add-to-list 'auto-mode-alist '("\\.json$" . json-mode))
#+END_SRC
*** Cosmetic changes
#+BEGIN_SRC emacs-lisp
  (dolist (mode '(js2-mode typescript-mode))
    (font-lock-add-keywords
     mode `( ;; Use right arrow for return in one-line functions
            ("\\(function\\) *\\*?("
             (0 (progn (compose-region (match-beginning 1)
                                       (match-end 1) "\u0192")
                       nil)))

            ;; Use lambda for anonymous functions
            ("function *([^)]*) *{ *\\(return\\) "
             (0 (progn (compose-region (match-beginning 1)
                                       (match-end 1) "\u2190")
                       nil)))
            ))
    (dolist (pair '(("=>" . "\u21D2")
                    ("===" . "\u2261")
                    ("!==" . "\u2262")
                    ("&&" . "\u22C0")
                    ("||" . "\u22C1")))
      (destructuring-bind (operator . char) pair
        (font-lock-add-keywords
         mode `((,operator
                 (0 (progn (compose-region (match-beginning 0)
                                           (match-end 0) ,char)
                           nil))))))))
#+END_SRC
*** Refactoring
#+BEGIN_SRC emacs-lisp
  (require-package 'js2-refactor)
  (require 'js2-refactor)

  (defadvice js2r-inline-var (after reindent-buffer activate)
    (cleanup-buffer))

  (js2r-add-keybindings-with-prefix "C-c C-m")
  (define-key js2-mode-map (kbd "M-SPC") 'js2r--ensure-just-one-space)
#+END_SRC
*** Code completion and analysis using Tern
#+BEGIN_SRC emacs-lisp
  (require-package 'tern)
  (require-package 'tern-auto-complete)
  (require 'tern)

;  (add-hook 'js2-mode-hook (lambda () (tern-mode t)))

  (eval-after-load 'tern
    '(progn
       (require 'tern-auto-complete)
       (tern-ac-setup)))
#+END_SRC

*** Web-mode for JSX
#+BEGIN_SRC emacs-lisp
  (require-package 'web-mode)
  (add-to-list 'auto-mode-alist '("\\.html?\\'" . web-mode))
  (add-to-list 'auto-mode-alist '("\\.css?\\'" . web-mode))

  (setq web-mode-markup-indent-offset 2
        web-mode-css-indent-offset 2
        web-mode-code-indent-offset 2
        web-mode-enable-auto-quoting nil)
#+END_SRC

*** DWIM new line indentation

#+BEGIN_SRC emacs-lisp
  (defun osener/newline-and-indent ()
    "Open a new brace or bracket expression, with relevant newlines and indent. "
    (interactive)
    (if (or (and (looking-back "(") (looking-at ")"))
            (and (looking-back "{") (looking-at "}")))
        (progn
          (newline-and-indent)
          (newline-and-indent)
          (forward-line -1)
          (indent-according-to-mode))
      (newline-and-indent)))

  (define-key js2-mode-map (kbd "RET") 'osener/newline-and-indent)
#+END_SRC

** Typescript
#+BEGIN_SRC emacs-lisp
  (require-package 'flymake-cursor)
  (require-package 'tss)
  (add-to-list 'auto-mode-alist '("\\.ts\\'" . typescript-mode))
  (setq tss-popup-help-key "C-c C-d")
  (setq tss-jump-to-definition-key "M-.")
  (tss-config-default)
#+END_SRC
** Scala
#+BEGIN_SRC emacs-lisp
  (require-package 'scala-mode2)

  (let ((default-directory "~/vcs/ensime"))
    (when (file-exists-p default-directory)
      (add-to-list 'load-path (expand-file-name "elisp/"))
      (require 'ensime)))
#+END_SRC
** Haskell
#+BEGIN_SRC emacs-lisp
  (require 'haskell-mode)
  (require 'haskell)
  (require 'haskell-mode)
  (require 'hindent)
  (require 'haskell-process)
  (require 'haskell-simple-indent)
  (require 'haskell-interactive-mode)
  (require 'haskell-font-lock)
  (require-package 'shm)
  (require 'shm)
  (require 'shm-case-split)
  (require 'shm-reformat)
#+END_SRC

ghc-mod
#+BEGIN_SRC emacs-lisp
  (require-package 'ghc)
  (autoload 'ghc-init "ghc" nil t)
  (add-hook 'haskell-mode-hook (lambda () (ghc-init)))
  (setq haskell-font-lock-symbols t)
#+END_SRC

hindent
#+BEGIN_SRC emacs-lisp
  (autoload 'hindent/reformat-decl "hindent" nil t)
  (setq-default hindent-style "chris-done")
#+END_SRC

company-ghc
#+BEGIN_SRC emacs-lisp
  (require-package 'company-ghc)
  (require 'company)
  (add-hook 'haskell-mode-hook 'company-mode)
  (add-hook 'haskell-mode-hook 'rainbow-delimiters-mode)
  (add-to-list 'company-backends 'company-ghc)
  (custom-set-variables '(company-ghc-show-info t))
#+END_SRC

Misc
#+BEGIN_SRC emacs-lisp
  (add-hook 'kill-emacs-hook
            (lambda ()
              (shell-command-to-string "killall hdevtools")))

  (custom-set-variables
   '(haskell-interactive-mode-eval-pretty nil)
   '(haskell-interactive-mode-include-file-name nil)
   '(haskell-notify-p t)
   '(haskell-process-args-cabal-repl
     '("--ghc-option=-ferror-spans" "--with-ghc=ghci-ng"))
   '(haskell-process-args-ghci '("-ferror-spans"))
   '(haskell-process-auto-import-loaded-modules t)
   '(haskell-process-log t)
   '(haskell-process-path-ghci "ghci-ng")
   '(haskell-process-reload-with-fbytecode nil)
   '(haskell-process-suggest-haskell-docs-imports t)
   '(haskell-process-suggest-remove-import-lines t)
   '(haskell-process-type 'cabal-repl)
   '(haskell-process-use-presentation-mode t)
   '(haskell-tags-on-save t)
   '(shm-auto-insert-bangs t)
   '(shm-auto-insert-skeletons t)
   '(shm-use-hdevtools nil)
   '(shm-use-presentation-mode t)
   '(haskell-complete-module-preferred
     '("Data.ByteString"
       "Data.ByteString.Lazy"
       "Data.Conduit"
       "Data.Function"
       "Data.List"
       "Data.Map"
       "Data.Maybe"
       "Data.Monoid"
       "Data.Ord")))


  (add-hook 'haskell-mode-hook (lambda () (electric-indent-mode 0)))
  (add-hook 'haskell-mode-hook (lambda () (auto-complete-mode 0)))
  (add-hook 'haskell-mode-hook 'structured-haskell-mode)
  (add-hook 'haskell-interactive-mode-hook 'structured-haskell-repl-mode)


  (defun haskell-interactive-toggle-print-mode ()
    (interactive)
    (setq haskell-interactive-mode-eval-mode
          (intern
           (ido-completing-read "Eval result mode: "
                                '("fundamental-mode"
                                  "haskell-mode"
                                  "espresso-mode"
                                  "ghc-core-mode"
                                  "org-mode")))))

  (defun haskell-insert-doc ()
    "Insert the documentation syntax."
    (interactive)
    (insert "-- | "))

  (defun haskell-insert-undefined ()
    "Insert undefined."
    (interactive)
    (if (and (boundp 'structured-haskell-mode)
             structured-haskell-mode)
        (shm-insert-string "undefined")
      (insert "undefined")))

  (defun haskell-move-right ()
    (interactive)
    (haskell-move-nested 1))

  (defun haskell-move-left ()
    (interactive)
    (haskell-move-nested -1))

  (defun haskell-who-calls (&optional prompt)
    "Grep the codebase to see who uses the symbol at point."
    (interactive "P")
    (let ((sym (if prompt
                   (read-from-minibuffer "Look for: ")
                 (haskell-ident-at-point))))
      (let ((existing (get-buffer "*who-calls*")))
        (when existing
          (kill-buffer existing)))
      (let ((buffer
             (grep-find (format "cd %s && find . -name '*.hs' -exec grep -inH -e %s {} +"
                                (haskell-session-current-dir (haskell-session))
                                sym))))
        (with-current-buffer buffer
          (rename-buffer "*who-calls*")
          (switch-to-buffer-other-window buffer)))))

  (defun haskell-auto-insert-module-template ()
    "Insert a module template for the newly created buffer."
    (interactive)
    (when (and (= (point-min)
                  (point-max))
               (buffer-file-name))
      (insert
       "-- | "
       "\n"
       "\n"
       "module "
       )
      (let ((name (haskell-guess-module-name)))
        (if (string= name "")
            (progn (insert "Main")
                   (shm-evaporate (- (point) 5)
                                  (point)))
          (insert name)))
      (insert " where"
              "\n"
              "\n")
      (goto-char (point-min))
      (forward-char 4)))

  ;; (define-key interactive-haskell-mode-map (kbd "C-?") 'haskell-mode-find-uses)
  (define-key interactive-haskell-mode-map (kbd "C-`") 'haskell-interactive-bring)
  (define-key interactive-haskell-mode-map (kbd "C-c C-c") 'haskell-process-cabal-build)
  (define-key interactive-haskell-mode-map (kbd "C-c C-k") 'haskell-interactive-mode-clear)
  (define-key interactive-haskell-mode-map (kbd "C-c C-l") 'haskell-process-load-or-reload)
  ;; (define-key interactive-haskell-mode-map (kbd "C-c C-t") 'haskell-mode-show-type-at)
  (define-key interactive-haskell-mode-map (kbd "C-c C-t") 'haskell-process-do-type)
  (define-key interactive-haskell-mode-map (kbd "C-c c") 'haskell-process-cabal)
  (define-key interactive-haskell-mode-map (kbd "M-,") 'haskell-who-calls)
  (define-key interactive-haskell-mode-map [f12] 'haskell-process-reload-devel-main)
  (define-key interactive-haskell-mode-map [f5] 'haskell-process-load-or-reload)

  (define-key haskell-mode-map (kbd "C-c i") 'hindent/reformat-decl)
  (define-key haskell-mode-map (kbd "C-c C-u") 'haskell-insert-undefined)
  (define-key haskell-mode-map (kbd "C-c C-a") 'haskell-insert-doc)
  (define-key haskell-mode-map (kbd "C-<return>") 'haskell-simple-indent-newline-indent)
  (define-key haskell-mode-map (kbd "C-<right>") 'haskell-move-right)
  (define-key haskell-mode-map (kbd "C-<left>") 'haskell-move-left)
  ;; (define-key haskell-mode-map (kbd "<space>") 'haskell-mode-contextual-space)

  (define-key haskell-cabal-mode-map (kbd "C-`") 'haskell-interactive-bring)
  (define-key haskell-cabal-mode-map [?\C-c ?\C-z] 'haskell-interactive-switch)
  (define-key haskell-cabal-mode-map (kbd "C-c C-c") 'haskell-process-cabal-build)
  (define-key haskell-cabal-mode-map (kbd "C-c c") 'haskell-process-cabal)
  (define-key haskell-cabal-mode-map (kbd "C-c C-k") 'haskell-interactive-mode-clear)

  (define-key haskell-interactive-mode-map (kbd "C-c C-v") 'haskell-interactive-toggle-print-mode)
  (define-key haskell-interactive-mode-map (kbd "C-c C-i") 'haskell-process-do-info)
  (define-key haskell-interactive-mode-map [f12] 'haskell-process-reload-devel-main)
  (define-key haskell-interactive-mode-map (kbd "C-<left>") 'haskell-interactive-mode-error-backward)
  (define-key haskell-interactive-mode-map (kbd "C-<right>") 'haskell-interactive-mode-error-forward)
  (define-key haskell-interactive-mode-map (kbd "C-c c") 'haskell-process-cabal)

  (define-key shm-map (kbd "C-c C-p") 'shm/expand-pattern)
  (define-key shm-map (kbd "C-c C-s") 'shm/case-split)
  ;; (define-key shm-map (kbd "SPC") 'shm-contextual-space)
  (define-key shm-map (kbd "C-\\") 'shm/goto-last-point)
  (define-key shm-map (kbd "C-c C-f") 'shm-fold-toggle-decl)
  (define-key shm-map (kbd "C-c i") 'shm-reformat-decl)
#+END_SRC

Ignore ghcjs output files
#+BEGIN_SRC emacs-lisp
  (mapc (lambda (ext) (push ext completion-ignored-extensions))
        '(".js_dyn_hi" ".js_dyn_o" ".js_hi" ".js_o"))

#+END_SRC
* Webdev
** Stylus
#+BEGIN_SRC emacs-lisp
  (require-package 'stylus-mode)
  (after-load 'auto-complete
    (add-hook 'stylus-mode-hook
              (lambda ()
                (ac-css-mode-setup)
                (rainbow-mode t))))
#+END_SRC
* Eshell
Great intro post for eshell, also the source of some of these settings:
http://www.masteringemacs.org/articles/2010/12/13/complete-guide-mastering-eshell/

** Change some defaults
#+BEGIN_SRC emacs-lisp
  (eval-after-load 'esh-opt
    '(progn
       (require 'em-prompt)
       (setq eshell-cmpl-ignore-case t)
       (setq eshell-prefer-lisp-functions t)
       (setq eshell-where-to-jump 'begin)
       (setq eshell-review-quick-commands nil)
       (setq eshell-smart-space-goes-to-end t)
       (setq eshell-directory-name
             (expand-file-name "./" (expand-file-name "eshell" user-emacs-directory)))))
#+END_SRC

** Convenience function for usage in a terminal emulator
This allows you to use eshell in a similar fashion to standard Unix
shells in a terminal emulator.

Call Emacs like this for an one-off eshell buffer:
emacsclient -a '' -t -e "(server-eshell)"
#+BEGIN_SRC emacs-lisp
  (defun server-eshell ()
    "Command to be called by emacs-client to start a new shell.

  A new eshell will be created. When the frame is closed, the buffer is
  deleted or the shell exits, then hooks will take care that the other
  actions happen. For example, when the frame is closed, then the buffer
  will be deleted and the client disconnected.

  Also creates a local binding of 'C-x #' to kill the buffer."
    (lexical-let ((buf (eshell t))
                  (client (first server-clients))
                  (frame (selected-frame)))
      (labels ((close (&optional arg)
                      (when (not (boundp 'cve/recurse))
                        (let ((cve/recurse t))
                          (delete-frame frame)
                          (kill-buffer buf)
                          (server-delete-client client)))))
        (add-hook 'eshell-exit-hook #'close t t)
        (add-hook 'delete-frame-functions #'close t t))
      (local-set-key (kbd "C-x #") (lambda () (interactive) (kill-buffer buf)))
      (delete-other-windows)
      nil))
#+END_SRC

** Quake-like eshell window
#+BEGIN_SRC emacs-lisp
  (require-package 'shell-pop)

  (setq shell-pop-window-position "bottom"
        shell-pop-window-height 50
        shell-pop-shell-type '("eshell" "*eshell*" (lambda () (eshell))))

  (global-set-key (kbd "<f8>") 'shell-pop)

#+END_SRC

** Easy way to open eshell in the directory of current buffer
[[https://github.com/technomancy/emacs-starter-kit/commit/c0e568d3c9940c9dd5241e4b49467723590fc2c2][From here]]
#+BEGIN_SRC emacs-lisp
(defun eshell-in-dir (&optional prompt)
  "Change the directory of an existing eshell to the directory of the file in
the current buffer or launch a new eshell if one isn't running. If the
current buffer does not have a file (e.g., a *scratch* buffer) launch or raise
eshell, as appropriate. Given a prefix arg, prompt for the destination
directory."
  (interactive "P")
  (let* ((original-buffer (current-buffer))
         (name (buffer-file-name))
         (dir (cond (prompt (read-directory-name "Directory: " nil nil t))
                    (name (file-name-directory name))
                    (t nil)))
         (buffers (delq nil (mapcar (lambda (buf)
                                      (with-current-buffer buf
                                        (when (eq 'eshell-mode major-mode)
                                          (buffer-name))))
                                    (buffer-list))))
         (buffer (cond ((eq 1 (length buffers)) (first buffers))
                       ((< 1 (length buffers)) (ido-completing-read
                                                "Eshell buffer: " buffers nil t
                                                nil nil (first buffers)))
                       (t (eshell)))))
    (with-current-buffer buffer
      (when dir
        (eshell/cd (list dir))
        (eshell-send-input))
      (end-of-buffer)
      (switch-to-buffer original-buffer)
      (shell-pop-up))))
#+END_SRC

** Clickable ls output
[[http://www.emacswiki.org/emacs/EshellEnhancedLS][From EmacsWiki]]
#+BEGIN_SRC emacs-lisp
  (eval-after-load "em-ls"
    '(progn
       (defun ted-eshell-ls-find-file-at-point (point)
         "RET on Eshell's `ls' output to open files."
         (interactive "d")
         (find-file
          (replace-regexp-in-string
           "[ \t\n]*$" ""
           (replace-regexp-in-string
            "^[ \t\n]*" ""
            (buffer-substring-no-properties
             (previous-single-property-change point 'help-echo)
             (next-single-property-change point 'help-echo))))))

       (defun pat-eshell-ls-find-file-at-mouse-click (event)
         "Middle click on Eshell's `ls' output to open files.
   From Patrick Anderson via the wiki."
         (interactive "e")
         (ted-eshell-ls-find-file-at-point (posn-point (event-end event))))

       (let ((map (make-sparse-keymap)))
         (define-key map (kbd "RET")      'ted-eshell-ls-find-file-at-point)
         (define-key map (kbd "<return>") 'ted-eshell-ls-find-file-at-point)
         (define-key map (kbd "<mouse-2>") 'pat-eshell-ls-find-file-at-mouse-click)
         (defvar ted-eshell-ls-keymap map))

       (defadvice eshell-ls-decorated-name (after ted-electrify-ls activate)
         "Eshell's `ls' now lets you click or RET on file names to open them."
         (add-text-properties 0 (length ad-return-value)
                              (list 'help-echo "RET, mouse-2: visit this file"
                                    'mouse-face 'highlight
                                    'keymap ted-eshell-ls-keymap)
                              ad-return-value)
         ad-return-value)))
#+END_SRC

** Colorize prompt on nonzero exit codes
#+BEGIN_SRC emacs-lisp
  (defface esk-eshell-error-prompt-face
    '((((class color) (background dark)) (:foreground "red" :bold t))
      (((class color) (background light)) (:foreground "red" :bold t)))
    "Face for nonzero prompt results"
    :group 'eshell-prompt)

  (add-hook 'eshell-after-prompt-hook
            (defun esk-eshell-exit-code-prompt-face ()
              (when (and eshell-last-command-status
                         (not (zerop eshell-last-command-status)))
                (let ((inhibit-read-only t))
                  (add-text-properties
                   (save-excursion (beginning-of-line) (point)) (point-max)
                   '(face esk-eshell-error-prompt-face))))))
#+END_SRC

** Misc commands
#+BEGIN_SRC emacs-lisp
  (defun eshell/cds ()
    "Change directory to the project's root."
    (eshell/cd (locate-dominating-file default-directory "src")))

  (defun eshell/cdl ()
    "Change directory to the project's root."
    (eshell/cd (locate-dominating-file default-directory "lib")))

  (defun eshell/cdg ()
    "Change directory to the project's root."
    (eshell/cd (locate-dominating-file default-directory ".git")))
#+END_SRC

* Term
Don't keep buffers with finished processes around
#+BEGIN_SRC emacs-lisp
  (defadvice term-sentinel (around my-advice-term-sentinel (proc msg))
    (if (memq (process-status proc) '(signal exit))
        (let ((buffer (process-buffer proc)))
          ad-do-it
          (kill-buffer buffer))
      ad-do-it))
  (ad-activate 'term-sentinel)
#+END_SRC

Yank into terminal with C-y
#+BEGIN_SRC emacs-lisp
  (add-hook 'term-mode-hook
            (lambda () (define-key term-raw-map (kbd "C-y") 'term-paste)))
#+END_SRC

Increase max buffer size
#+BEGIN_SRC emacs-lisp
  (add-hook 'term-mode-hook
            (lambda ()
              (setq term-buffer-maximum-size 10000)))
#+END_SRC

Multi-term
#+BEGIN_SRC emacs-lisp
  (require-package 'multi-term)

  (setq multi-term-program "/usr/local/bin/fish")

  (add-hook 'term-mode-hook
            (lambda ()
              (setq term-bind-key-alist
                    (append term-bind-key-alist
                            '(("M-[" . multi-term-prev)
                              ("M-]" . multi-term-next)
                              ("M-d" . term-send-forward-kill-word)
                              ("M-DEL" . term-send-forward-kill-word)
                              ("C-DEL" . term-send-forward-kill-word)
                              ("M-<backspace>" . term-send-backward-kill-word)
                              ("C-<backspace>" . term-send-backward-kill-word)
                              ("M-<right>" . term-send-forward-word)
                              ("C-<right>" . term-send-forward-word)
                              ("M-<left>" . term-send-backward-word)
                              ("C-<left>" . term-send-backward-word))))))
#+END_SRC

* Dired
Custom ls invocation
#+BEGIN_SRC emacs-lisp
  (setq dired-listing-switches
        "-aGhlvF --group-directories-first --time-style=long-iso")
#+END_SRC

Try guessing dired targets
#+BEGIN_SRC emacs-lisp
  (setq dired-dwim-target t)
#+END_SRC

Don't ask before doing recursive copies
#+BEGIN_SRC emacs-lisp
  (setq dired-recursive-copies 'always)
#+END_SRC

* Navigation
Goto line with feedback
#+BEGIN_SRC emacs-lisp
  (global-set-key [remap goto-line] 'goto-line-with-feedback)

  (defun goto-line-with-feedback ()
    "Show line numbers temporarily, while prompting for the line number input"
    (interactive)
    (if (and (boundp 'linum-mode)
             linum-mode)
        (call-interactively 'goto-line)
      (unwind-protect
          (progn
            (linum-mode 1)
            (call-interactively 'goto-line))
        (linum-mode -1))))
#+END_SRC

Projectile
#+BEGIN_SRC emacs-lisp
  (require-package 'projectile)
  (require-package 'helm-projectile)

  (projectile-global-mode t)
#+END_SRC
* Editing
#+BEGIN_SRC emacs-lisp
  (require-package 'zop-to-char)
  (global-set-key (kbd "M-z") 'zop-to-char)
#+END_SRC
* Window management
Popwin
#+BEGIN_SRC emacs-lisp
  (require-package 'popwin)

  (require 'popwin)
  (setq display-buffer-function 'popwin:display-buffer)

  (setq popwin:special-display-config
        '(("*Help*" :height 30 :stick t)
          ("*Completions*" :noselect t)
          ("*compilation*" :noselect t)
          ("*Messages*" :height 30)
          ("*Directory*" :noselect t)
          ("*Packages*" :height 30)
          ("\\*Slime Description.*" :noselect t :regexp t :height 30)
          ("*magit-commit*" :noselect t :height 40 :width 80)
          ("*magit-diff*" :noselect t :height 40 :width 80)
          ("*magit-edit-log*" :noselect t :height 15 :width 80)
          ("\\*Slime Inspector.*" :regexp t :height 30)
          ("*Ido Completions*" :noselect t :height 30)
          ("\\*ansi-term\\*.*" :regexp t :height 30)
          ("*shell*" :height 30)
          ("*gists*" :height 30)
          ("*sldb.*":regexp t :height 30)
          ("*nrepl-error*" :noselect t)))
#+END_SRC
Rotate windows
#+BEGIN_SRC emacs-lisp
  (defun rotate-windows ()
    "Rotate your windows"
    (interactive)
    (cond ((not (> (count-windows)1))
           (message "You can't rotate a single window!"))
          (t
           (setq i 1)
           (setq numWindows (count-windows))
           (while  (< i numWindows)
             (let* (
                    (w1 (elt (window-list) i))
                    (w2 (elt (window-list) (+ (% i numWindows) 1)))

                    (b1 (window-buffer w1))
                    (b2 (window-buffer w2))

                    (s1 (window-start w1))
                    (s2 (window-start w2))
                    )
               (set-window-buffer w1  b2)
               (set-window-buffer w2 b1)
               (set-window-start w1 s2)
               (set-window-start w2 s1)
               (setq i (1+ i)))))))
#+END_SRC

Toggle window split
#+BEGIN_SRC emacs-lisp
  (defun toggle-window-split ()
    (interactive)
    (if (= (count-windows) 2)
        (let* ((this-win-buffer (window-buffer))
               (next-win-buffer (window-buffer (next-window)))
               (this-win-edges (window-edges (selected-window)))
               (next-win-edges (window-edges (next-window)))
               (this-win-2nd (not (and (<= (car this-win-edges)
                                           (car next-win-edges))
                                       (<= (cadr this-win-edges)
                                           (cadr next-win-edges)))))
               (splitter
                (if (= (car this-win-edges)
                       (car (window-edges (next-window))))
                    'split-window-horizontally
                  'split-window-vertically)))
          (delete-other-windows)
          (let ((first-win (selected-window)))
            (funcall splitter)
            (if this-win-2nd (other-window 1))
            (set-window-buffer (selected-window) this-win-buffer)
            (set-window-buffer (next-window) next-win-buffer)
            (select-window first-win)
            (if this-win-2nd (other-window 1))))))
#+END_SRC

Easy window navigation using M-1..M-0
#+BEGIN_SRC emacs-lisp
  (require-package 'window-numbering)
  (window-numbering-mode t)
#+END_SRC

Keybindings
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-x -") 'rotate-windows)
  (global-set-key (kbd "C-x C--") 'toggle-window-split)
  (global-unset-key (kbd "C-x C-+")) ;; don't zoom like this
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun toggle-fullscreen ()
    "Toggle full screen"
    (interactive)
    (set-frame-parameter
     nil 'fullscreen
     (when (not (frame-parameter nil 'fullscreen)) 'fullboth)))

  (global-set-key (kbd "<f11>") 'toggle-fullscreen)
#+END_SRC

* Mail
#+BEGIN_SRC emacs-lisp
  (require 'gnus)
  (define-key gnus-group-mode-map (kbd "o") 'gnus-group-list-all-groups)

  (setq user-mail-address "ozan@ozansener.com"
        user-full-name  "Ozan Sener")

  (setq message-send-mail-function 'smtpmail-send-it
        smtpmail-stream-type 'starttls
        smtpmail-default-smtp-server "smtp.gmail.com"
        smtpmail-smtp-server "smtp.gmail.com"
        smtpmail-smtp-service 587)

  (add-hook 'message-send-hook
            (lambda ()
              (unless (yes-or-no-p "Sure you want to send this?")
                (signal 'quit nil))))


#+END_SRC

Mu4e
#+BEGIN_SRC emacs-lisp
  (add-to-list 'load-path "/run/current-system/sw/share/emacs/site-lisp/mu4e/")
  (require 'mu4e)


  (add-hook 'mu4e-view-mode-hook (lambda () (visual-line-mode)))
  (when (fboundp 'imagemagick-register-types)
    (imagemagick-register-types))

  (defun mu4e-shr2text ()
    (let ((dom (libxml-parse-html-region (point-min) (point-max))))
      (erase-buffer)
      (shr-insert-document dom)
      (goto-char (point-min))))
  (defun oni:shr-colorize-remove-last-arg (args)
    "If ARGS has more than 3 items, remove the last one."
    (if (> (length args) 3)
        (butlast args)
      args))
  (with-eval-after-load 'shr
    (advice-add #'shr-colorize-region :filter-args
                #'oni:shr-colorize-remove-last-arg))

  ;; Maildirs extension
  (require-package 'mu4e-maildirs-extension)
  (require 'mu4e-maildirs-extension)
  (mu4e-maildirs-extension)

  (setq mu4e-maildir "~/.mail"
        mu4e-sent-folder "/sent"
        mu4e-drafts-folder "/drafts"
        mu4e-trash-folder "/trash"
        mu4e-get-mail-command "offlineimap"
        mu4e-update-interval nil
        mu4e-sent-messages-behavior 'delete
        mu4e-headers-include-related nil
        mu4e-headers-skip-duplicates t
        mu4e-confirm-quit nil
        mu4e-maildir-shortcuts'(("/INBOX" . ?i))
        mu4e-html2text-command 'mu4e-shr2text
        mu4e-view-prefer-html t
        mu4e-view-show-images t
        mu4e-use-fancy-chars t
        mu4e-update-interval 300
        mu4e-skip-duplicates t
        mu4e-compose-signature
        (concat
         "Ozan Sener\n"
         "http://www.ozansener.com\n"))
#+END_SRC

* Chat
** ERC
Unique nick colorization
#+BEGIN_SRC emacs-lisp
  (require-package 'erc-hl-nicks)
#+END_SRC

Disable trailing whitespace in ERC buffers
#+BEGIN_SRC emacs-lisp
  (add-hook 'erc-mode-hook (lambda ()
                                (setq show-trailing-whitespace nil)))
#+END_SRC

* Helm
#+BEGIN_SRC emacs-lisp
  (require-package 'helm)
  (require-package 'helm-swoop)

  (require 'helm-config)

  (helm-mode -1)

  (setq helm-split-window-in-side-p t)
#+END_SRC

Keybindings
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "<f1>") 'helm-mini)
  (global-set-key (kbd "M-i") 'helm-swoop)
  (global-set-key (kbd "M-I") 'helm-swoop-back-to-last-point)
  (define-key org-mode-map (kbd "C-x TAB") 'helm-org-headlines)
  (global-set-key (kbd "C-x TAB") 'helm-imenu)
#+END_SRC

* Git
Toggle whitespace in magit-status buffers
#+BEGIN_SRC emacs-lisp
  (require 'magit)

  (defun magit-toggle-whitespace ()
    (interactive)
    (if (member "-w" magit-diff-options)
        (magit-dont-ignore-whitespace)
      (magit-ignore-whitespace)))

  (defun magit-ignore-whitespace ()
    (interactive)
    (add-to-list 'magit-diff-options "-w")
    (magit-refresh))

  (defun magit-dont-ignore-whitespace ()
    (interactive)
    (setq magit-diff-options (remove "-w" magit-diff-options))
    (magit-refresh))

  (global-set-key (kbd "C-c g") 'magit-status)
  (define-key magit-status-mode-map (kbd "W") 'magit-toggle-whitespace)
#+END_SRC

Add keybinding for toggling --no-merges in log view
#+BEGIN_SRC emacs-lisp
  (eval-after-load 'magit-key-mode
    '(magit-key-mode-insert-switch 'logging "-l" "No merges" "--no-merges"))
#+END_SRC

Diff-hl mode
#+BEGIN_SRC emacs-lisp
  (require-package 'diff-hl)
  (global-diff-hl-mode t)
#+END_SRC

Style-checking for git commit messages
#+BEGIN_SRC emacs-lisp
  (require-package 'git-commit-mode)
#+END_SRC

Misc
#+BEGIN_SRC emacs-lisp
  (setq magit-diff-refine-hunk 'all)
  (setq magit-repo-dirs '("~/vcs"))
#+END_SRC

* OSX related tweaks
Input related tweaks
#+BEGIN_SRC emacs-lisp
  (when *is-a-mac*
    (setq mac-command-modifier 'super)
    (setq mac-option-modifier 'meta)
    (global-unset-key (kbd "s-q")))
#+END_SRC

* Cosmetic changes
Set default font
#+BEGIN_SRC emacs-lisp
  (add-to-list 'default-frame-alist
               `(font . ,(if *is-a-mac*
                            "PragmataPro-15"
                            "PragmataPro-12")))
#+END_SRC

Disable bold faces
#+BEGIN_SRC emacs-lisp
  (mapc
   (lambda (face)
     (set-face-attribute face nil :weight 'normal :underline nil))
   (face-list))
#+END_SRC

Use UTF-8 bullets on org-mode buffers
#+BEGIN_SRC emacs-lisp
  (require-package 'org-bullets)
  (add-hook 'org-mode-hook (lambda () (org-bullets-mode 1)))
#+END_SRC

Fontify code blocks in org-mode buffers
#+BEGIN_SRC emacs-lisp
  (setq org-src-fontify-natively t)
#+END_SRC

Turn on `org-indent-mode' on startup
#+BEGIN_SRC emacs-lisp
  (setq org-startup-indented t)
#+END_SRC

Parse ansi color escape codes in compilation-mode buffers
#+BEGIN_SRC emacs-lisp
  (require 'ansi-color)
  (defun colorize-compilation-buffer ()
    (toggle-read-only)
    (ansi-color-apply-on-region (point-min) (point-max))
    (toggle-read-only))
  (add-hook 'compilation-filter-hook 'colorize-compilation-buffer)
#+END_SRC

Indent guide
#+BEGIN_SRC emacs-lisp
  (require-package 'indent-guide)
  (require 'indent-guide)
  (add-hook 'stylus-mode-hook 'indent-guide-mode)
#+END_SRC

No blinking!
#+BEGIN_SRC emacs-lisp
  (blink-cursor-mode -1)
#+END_SRC

Increase fringe width
#+BEGIN_SRC emacs-lisp
  (fringe-mode 8)
#+END_SRC

** Fancy mode-line
#+BEGIN_SRC emacs-lisp
  (require-package 'svg-mode-line-themes)

  (require 'svg-mode-line-themes)

  (smt/enable)

  (defun smt/buffer-indicators-text (widget)
    (if buffer-read-only " RO " " RW "))

  (defun smt/buffer-name-text (widget)
    (format-mode-line "%b"))

  (defun smt/minor-mode-indicator-text (widget)
    (concat
     (when defining-kbd-macro                             " REC ")
     (when (bound-and-true-p aai-mode)                    " I ")
     (when (or (bound-and-true-p evil-local-mode)
               (bound-and-true-p evil-mode))              " [eVil] ")
     (when (bound-and-true-p dired-omit-mode)             " O ")
     (when (bound-and-true-p save-auto-hook)              " A ")
     (when (/= (- (point-max) (point-min)) (buffer-size)) " N ")
     (when (bound-and-true-p wmi)                         " M ")
     (when (bound-and-true-p multiple-cursors-mode)       " mc ")
     (when (bound-and-true-p iedit-mode)                  " iedit ")))

  (smt/defwidget buffer-dirty
    :text (lambda (widget)
            (if (and
                 (buffer-modified-p)
                 (or buffer-file-name buffer-offer-save))
                " ❉ " " ✓ ")))

  (smt/defwidget position-info
    :text (lambda (widget)
            (format-mode-line "%l:%c [%p] %I"))
    :on-click (lambda (widget event)
                (what-cursor-position t)))

  (smt/defwidget major-mode
    :text (lambda (widget)
            (format-mode-line mode-name))
    :on-click (lambda (widget event)
                (message "%s" (format-mode-line mode-line-modes))))

  (smt/defrow default-position
    :widgets '(position-info)
    :align "right"
    :margin 5)

  (smt/defrow default-left
    :widgets '(buffer-info buffer-name buffer-dirty which-function)
    :margin 10)

  (smt/defrow default-right
    :widgets '(major-mode version-control minor-modes)
    :align "right"
    :margin 30)

  (defun ocodo:smt/background (theme)
    (let ((width (smt/window-pixel-width))
          (height (smt/t-pixel-height theme)))
      `((\defs
         (linearGradient
          :id "twisted" :x1 "0%" :y1 "0%" :x2 "100%" :y2 "25%"
          (stop :offset "0%"  :style "stop-color:#000000;stop-opacity:0.3")
          (stop :offset "25%"  :style "stop-color:#484848;stop-opacity:0.3")
          (stop :offset "75%"  :style "stop-color:#484848;stop-opacity:0.3")
          (stop :offset "100%"  :style "stop-color:#000000;stop-opacity:0.3"))
         (filter
          :id "blur"
          (feGaussianBlur
           :stdDeviation "5")
          (feComposite)))
        (rect :width "100%" :height "100%" :x 0 :y 0 :fill "url(#twisted)" :fill-opacity 1)
        (rect :width "100%" :height 1 :x 0 :y height :fill "#383838" :fill-opacity 1)

        (g :transform ,(format "translate(%i,0)" (- width 80))
           (g :transform "matrix(1.25,0,0,-1.25,0,32)"
              (g :transform "matrix(0.14833729,0,0,0.14833729,31.537529,25.978459)"    (path :fill "#6cb681" :fill-opacity 0.6 :d "M 0,0 39.208,-61.232 78.417,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,43.16965,25.978459)"     (path :fill "#62b286" :fill-opacity 0.6 :d "m 0,0 -39.208,-61.232 83.937,0 L 5.521,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,43.988605,25.978459)"    (path :fill "#aabd86" :fill-opacity 0.6 :d "M 0,0 39.208,-61.232 78.418,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,55.620993,25.978459)"    (path :fill "#bfb980" :fill-opacity 0.6 :d "m 0,0 -39.21,-61.232 83.938,0 L 5.521,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,56.439934,25.978459)"    (path :fill "#e2ae6e" :fill-opacity 0.6 :d "M 0,0 39.208,-61.232 68.317,-15.771 68.317,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,62.255883,16.89541)"     (path :fill "#e69c5e" :fill-opacity 0.6 :d "m 0,0 29.11,0 0,45.461 L 0,0 z"))

              (g :transform "matrix(0.14833729,0,0,0.14833729,31.128089,7.1725684)"    (path :fill "#028f85" :fill-opacity 0.6 :d "M 0,0 41.969,65.545 83.938,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,49.804673,16.895395)"    (path :fill "#3f9f88" :fill-opacity 0.6 :d "M 0,0 -41.969,-65.545 -83.938,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,43.57915,7.1725684)"     (path :fill "#72aa88" :fill-opacity 0.6 :d "M 0,0 41.969,65.545 83.939,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,62.255883,16.895395)"    (path :fill "#a5b785" :fill-opacity 0.6 :d "M 0,0 -41.968,-65.545 -83.938,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,56.030478,7.1726129)"    (path :fill "#c9cb87" :fill-opacity 0.6 :d "m 0,0 71.078,0 0,20.083 L 41.968,65.545 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,62.255883,16.89541)"     (path :fill "#e7bd73" :fill-opacity 0.6 :d "M 0,0 29.11,-45.462 29.11,0 0,0 z"))

              (g :transform "matrix(0.14833729,0,0,0.14833729,31.128059,7.1726129)"    (path :fill "#007d7e" :fill-opacity 0.6 :d "m 0,0 30.961,-48.353 22.016,0 L 83.938,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,38.986494,5.9236201e-7)" (path :fill "#69a489" :fill-opacity 0.6 :d "M 0,0 61.922,0 30.961,48.353 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,43.579135,7.1726129)"    (path :fill "#8bb285" :fill-opacity 0.6 :d "m 0,0 30.961,-48.353 22.016,0 L 83.939,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,51.437629,5.9236201e-7)" (path :fill "#bec680" :fill-opacity 0.6 :d "M 0,0 61.922,0 30.962,48.353 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,56.030478,7.1726129)"    (path :fill "#d7b866" :fill-opacity 0.6 :d "m 0,0 30.96,-48.353 22.016,0 18.102,28.27 L 71.078,0 0,0 z"))
              (g :transform "matrix(0.14833729,0,0,0.14833729,63.88878,5.9236201e-7)"  (path :fill "#eeb767" :fill-opacity 0.6 :d "m 0,0 18.102,0 0,28.27 L 0,0 z"))))

        (g :transform "matrix(1.25,0,0,-1.25,0,32)"
           (g :transform "matrix(-0.14833729,0,0,0.14833729,105.25468,-47.37267)"
              (g :transform "translate(299.9707,494.4888)" (path :fill "#6cb681" :fill-opacity 0.6  :d "m 0,0 -39.209,-61.232 83.939,0 L 5.521,0 0,0 z" ))
              (g :transform "translate(305.4917,494.4888)" (path :fill "#62b286" :fill-opacity 0.6  :d "M 0,0 39.209,-61.232 78.418,0 0,0 z" ))
              (g :transform "translate(383.9101,494.4888)" (path :fill "#aabd86" :fill-opacity 0.6  :d "m 0,0 -39.209,-61.232 83.938,0 L 5.521,0 0,0 z" ))
              (g :transform "translate(389.431,494.4888)"  (path :fill "#bfb980" :fill-opacity 0.6  :d "M 0,0 39.208,-61.232 78.417,0 0,0 z" ))
              (g :transform "translate(467.8478,494.4888)" (path :fill "#e2ae6e" :fill-opacity 0.6  :d "m 0,0 -39.208,-61.232 83.937,0 L 5.521,0 0,0 z" ))
              (g :transform "translate(473.3686,494.4888)" (path :fill "#e69c5e" :fill-opacity 0.6  :d "M 0,0 39.208,-61.232 78.417,0 0,0 z" ))
              (g :transform "translate(551.7853,494.4888)" (path :fill "#e08951" :fill-opacity 0.6  :d "m 0,0 -39.208,-61.232 83.937,0 L 5.521,0 0,0 z" ))
              (g :transform "translate(557.3062,494.4888)" (path :fill "#f2a047" :fill-opacity 0.6  :d "M 0,0 39.208,-61.232 78.418,0 0,0 z" ))
              (g :transform "translate(635.7247,494.4888)" (path :fill "#e57d50" :fill-opacity 0.6  :d "m 0,0 -39.21,-61.232 83.938,0 L 5.521,0 0,0 z" ))
              (g :transform "translate(641.2455,494.4888)" (path :fill "#c14b47" :fill-opacity 0.6  :d "M 0,0 39.208,-61.232 68.317,-15.771 68.317,0 0,0 z" ))
              (g :transform "translate(680.4531,433.2564)" (path :fill "#b63840" :fill-opacity 0.6  :d "m 0,0 29.11,0 0,45.461 L 0,0 z" ))

              (g :transform "translate(344.7012,433.2563)" (path :fill "#028f85" :fill-opacity 0.6  :d "M 0,0 -41.97,-65.545 -83.939,0 0,0 z" ))
              (g :transform "translate(302.731,367.7109)"  (path :fill "#3f9f88" :fill-opacity 0.6  :d "M 0,0 41.97,65.545 83.94,0 0,0 z" ))
              (g :transform "translate(428.6396,433.2563)" (path :fill "#72aa88" :fill-opacity 0.6  :d "M 0,0 -41.969,-65.545 -83.938,0 0,0 z" ))
              (g :transform "translate(386.6709,367.7109)" (path :fill "#a5b785" :fill-opacity 0.6  :d "M 0,0 41.969,65.545 83.938,0 0,0 z" ))
              (g :transform "translate(512.5771,433.2563)" (path :fill "#c9cb87" :fill-opacity 0.6  :d "M 0,0 -41.969,-65.545 -83.938,0 0,0 z" ))
              (g :transform "translate(470.6084,367.7109)" (path :fill "#e7bd73" :fill-opacity 0.6  :d "M 0,0 41.969,65.545 83.938,0 0,0 z" ))
              (g :transform "translate(596.5146,433.2563)" (path :fill "#f6c576" :fill-opacity 0.6  :d "M 0,0 -41.969,-65.545 -83.938,0 0,0 z" ))
              (g :transform "translate(554.5459,367.7109)" (path :fill "#eea258" :fill-opacity 0.6  :d "M 0,0 41.969,65.545 83.939,0 0,0 z" ))
              (g :transform "translate(680.4531,433.2563)" (path :fill "#c76b4d" :fill-opacity 0.6  :d "M 0,0 -41.968,-65.545 -83.938,0 0,0 z" ))
              (g :transform "translate(638.4852,367.7112)" (path :fill "#b14f43" :fill-opacity 0.6  :d "m 0,0 71.078,0 0,20.083 L 41.968,65.545 0,0 z" ))
              (g :transform "translate(680.4531,433.2564)" (path :fill "#963336" :fill-opacity 0.6  :d "M 0,0 29.11,-45.462 29.11,0 0,0 z" ))

              (g :transform "translate(271.7697,319.3578)" (path :fill "#00707a" :fill-opacity 0.6  :d "M 0,0 61.923,0 30.962,48.353 0,0 z" ))
              (g :transform "translate(302.7312,367.7112)" (path :fill "#007d7e" :fill-opacity 0.6  :d "m 0,0 30.961,-48.353 22.017,0 L 83.939,0 0,0 z" ))
              (g :transform "translate(355.7091,319.3578)" (path :fill "#69a489" :fill-opacity 0.6  :d "M 0,0 61.922,0 30.961,48.353 0,0 z" ))
              (g :transform "translate(386.6706,367.7112)" (path :fill "#8bb285" :fill-opacity 0.6  :d "m 0,0 30.961,-48.353 22.016,0 L 83.938,0 0,0 z" ))
              (g :transform "translate(439.6474,319.3578)" (path :fill "#bec680" :fill-opacity 0.6  :d "M 0,0 61.922,0 30.961,48.353 0,0 z" ))
              (g :transform "translate(470.6082,367.7112)" (path :fill "#d7b866" :fill-opacity 0.6  :d "m 0,0 30.961,-48.353 22.016,0 L 83.938,0 0,0 z" ))
              (g :transform "translate(523.585,319.3578)"  (path :fill "#eeb767" :fill-opacity 0.6  :d "M 0,0 61.922,0 30.961,48.353 0,0 z" ))
              (g :transform "translate(554.5458,367.7112)" (path :fill "#d68651" :fill-opacity 0.6  :d "m 0,0 30.961,-48.353 22.016,0 L 83.939,0 0,0 z" ))
              (g :transform "translate(607.523,319.3578)"  (path :fill "#c16448" :fill-opacity 0.6  :d "M 0,0 61.922,0 30.962,48.353 0,0 z" ))
              (g :transform "translate(638.4852,367.7112)" (path :fill "#9b4641" :fill-opacity 0.6  :d "m 0,0 30.96,-48.353 22.016,0 18.102,28.27 L 71.078,0 0,0 z" ))
              (g :transform "translate(691.4611,319.3578)" (path :fill "#83373c" :fill-opacity 0.6  :d "m 0,0 18.102,0 0,28.27 L 0,0 z" )))))))


  (defun ocodo:smt/overlay (theme)
    (let ((width (smt/window-pixel-width))
          (height (smt/t-pixel-height theme)))
      `((\defs
         (linearGradient
          :id "over-gradient" :x1 "0%" :y1 "0%" :x2 "0%" :y2 "100%"
          (stop :offset "0%" :style "stop-color:#FFFFFF;stop-opacity:0.1")
          (stop :offset "20%" :style "stop-color:#000000;stop-opacity:0.0")
          (stop :offset "90%" :style "stop-color:#000000;stop-opacity:0.5")
          (stop :offset "100%" :style "stop-color:#000000;stop-opacity:0.8")))
        (rect :width "100%" :height "100%" :x 0 :y 0 :fill "url(#over-gradient)"))))

  (defun smt/ocodo-title-style (widget)
    (list :font-weight "normal"
          :font-size "12pt"
          :font-family "sans-serif"
          :filter nil
          :fill (if (smt/window-active-p)
                    "#FFFFFF"
                  "#666666")))

  (defun smt/ocodo-major-mode-style (widget)
    (list :font-weight "normal"
          :font-size "12pt"
          :filter nil
          :font-family "sans-serif"
          :fill (if (smt/window-active-p)
                    "#AAAAAA"
                  "#666666")))

  (defun smt/ocodo-info-style (widget)
    (list :font-weight "normal"
          :font-size "10pt"
          :filter nil
          :font-family "sans-serif"
          :fill (if (smt/window-active-p)
                    "#999999"
                  "#555555")))

  (defun smt/ocodo-position-info-style (widget)
    (list :font-weight "normal"
          :font-size "11pt"
          :filter nil
          :fill (if (smt/window-active-p)
                    "#DDDDDD"
                  "#999999")))

  (defun smt/ocodo-dirty-style (widget)
    (list :font-weight "normal"
          :font-size "11pt"
          :filter nil
          :font-family "sans-serif"
          :fill (if (and (or buffer-file-name buffer-offer-save) (buffer-modified-p))
                    ;; Dirty
                    (if (smt/window-active-p)
                        "#FF6060" "#763030")
                  ;; Untouched
                  (if (smt/window-active-p)
                      "#1F4F25" "#143519"))))

  (defun smt/ocodo-minor-mode-style (widget)
    (list :font-weight "normal"
          :font-size "11pt"
          :filter nil
          :fill (if (smt/window-active-p)
                    "#FFFFFF"
                  "#666666")))

  (defun smt/ocodo-version-control-style (widget)
    (list :font-weight "bold"
          :font-size "11pt"
          :filter nil
          :font-family "sans-serif"
          :fill (if (smt/window-active-p)
                    "#60B18C"
                  "#666666")))

  (smt/deftheme ocodo:smt
    :pixel-height 32
    :background 'ocodo:smt/background
    :overlay    'ocodo:smt/overlay
    :local-widgets
    (list (cons 'major-mode
                (smt/make-widget
                 :prototype 'major-mode
                 :style 'smt/ocodo-major-mode-style))

          (cons 'minor-modes
                (smt/make-widget
                 :prototype 'minor-modes
                 :style 'smt/ocodo-minor-mode-style))

          (cons 'version-control
                (smt/make-widget
                 :prototype 'version-control
                 :style 'smt/ocodo-version-control-style))

          (cons 'position-info
                (smt/make-widget
                 :prototype 'position-info
                 :style 'smt/ocodo-position-info-style))

          (cons 'buffer-info
                (smt/make-widget
                 :prototype 'buffer-info
                 :style 'smt/ocodo-info-style))

          (cons 'buffer-dirty
                (smt/make-widget
                 :prototype 'buffer-dirty
                 :style 'smt/ocodo-dirty-style))

          (cons 'buffer-name
                (smt/make-widget
                 :prototype 'buffer-name
                 :style 'smt/ocodo-title-style)))

    :rows (list
           'default-left
           'default-position
           'default-right))


  (let ((theme (cdr (assoc 'archetype smt/themes)))
        (row (cdr (assoc 'archetype smt/rows))))

    ;; Customise to use your desired default font
    (setf (getf theme :style) (list :font-size "12pt" :font-family "sans-serif"))

    (setf (getf row :baseline) 19))

  (smt/set-theme 'ocodo:smt)
  (set-face-attribute 'mode-line nil :box nil)
  (set-face-attribute 'mode-line-inactive nil :box nil)

#+END_SRC
* Misc Emacs modes
M-x find-file http://en.wikipedia.org/wiki/Main_Page
#+BEGIN_SRC emacs-lisp
  (url-handler-mode t)
#+END_SRC

Edit current file with sudo
#+BEGIN_SRC emacs-lisp
  (defun edit-file-with-sudo ()
    "Open the currently visited file as root via sudo."
    (interactive)
    (if (buffer-file-name)
        (let ((file-name (buffer-file-name)))
          (kill-buffer (current-buffer))
          (find-file (concat "/sudo::" file-name))
          (message "now editing %s as root" file-name))))

#+END_SRC

Clean old buffers
#+BEGIN_SRC emacs-lisp
  (autoload 'clean-buffer-list "midnight"
    "Kill old buffers that have not been displayed recently." t)
#+END_SRC

** Smartparens
#+BEGIN_SRC emacs-lisp
  (require-package 'smartparens)
  (setq sp-base-key-bindings 'paredit)
  (smartparens-global-strict-mode t)
  (diminish 'smartparens-mode)
  (require 'smartparens-config)
#+END_SRC

Temporary fix for missing cua-replace-region (See [[https://github.com/Fuco1/smartparens/issues/271][#271]])
#+BEGIN_SRC emacs-lisp
  (unless (fboundp 'cua-replace-region)
    (defun cua-replace-region ()
      "Replace the active region with the character you type."
      (interactive)
      (let ((not-empty (and cua-delete-selection (cua-delete-region))))
        (unless (eq this-original-command this-command)
          (let ((overwrite-mode
                 (and overwrite-mode
                      not-empty
                      (not (eq this-original-command 'self-insert-command)))))
            (cua--fallback))))))
#+END_SRC

** Convenient bookmarking using bm
#+BEGIN_SRC emacs-lisp
  (require-package 'bm)

  (global-set-key (kbd "<M-f2>") 'bm-toggle)
  (global-set-key (kbd "<f2>")   'bm-next)
  (global-set-key (kbd "<S-f2>") 'bm-previous)
  (global-set-key (kbd "<left-fringe> <mouse-1>") 'bm-toggle-mouse)
  (global-set-key (kbd "<left-fringe> <wheel-down>") 'bm-next-mouse)
  (global-set-key (kbd "<left-fringe> <wheel-up>") 'bm-previous-mouse)
#+END_SRC

** Browsers
#+BEGIN_SRC emacs-lisp
  (require-package 'w3m)
  (setq browse-url-browser-function 'browse-url-default-browser)
#+END_SRC

** Disable hl-line-mode
#+BEGIN_SRC emacs-lisp
  (remove-hook 'prog-mode-hook 'esk-turn-on-hl-line-mode)
#+END_SRC

** ag
Editable buffer
#+BEGIN_SRC emacs-lisp
  (require-package 'wgrep-ack)
  (add-hook 'ag-mode-hook 'wgrep-ack-and-a-half-setup)
  (setq wgrep-auto-save-buffer t)
  (setq wgrep-enable-key "w")
  (setq wgrep-change-readonly-file t)
#+END_SRC

Ignore ghcjs output files
#+BEGIN_SRC emacs-lisp
  (setq ag-ignore-list '(".js_" "*.jsexe*"))
#+END_SRC
** Shell
Use fish shell
#+BEGIN_SRC emacs-lisp
  (setq explicit-shell-file-name "/usr/local/bin/fish")
#+END_SRC

** Snippets
#+BEGIN_SRC emacs-lisp
  (require-package 'yasnippet)
#+END_SRC

Enable globally
#+BEGIN_SRC emacs-lisp
  (yas-global-mode 1)
#+END_SRC

Disable in some modes
#+BEGIN_SRC emacs-lisp
  (add-hook 'term-mode-hook (lambda ()
                              (yas-minor-mode -1)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (diminish 'yas-minor-mode)
#+END_SRC

Prefer IDO prompt
#+BEGIN_SRC emacs-lisp
  (setq yas/prompt-functions '(yas/ido-prompt yas/dropdown-prompt))
#+END_SRC



*** Helper functions
JavaScript [[https://github.com/magnars/.emacs.d/blob/master/defuns/snippet-helpers.el][(From here)]]
#+BEGIN_SRC emacs-lisp
  (defun js-method-p ()
    (save-excursion
      (word-search-backward "function")
      (looking-back ": ")))

  (defun js-function-declaration-p ()
    (save-excursion
      (word-search-backward "function")
      (looking-back "^\\s *")))

  (defun snippet--function-punctuation ()
    (if (js-method-p)
        (when (not (looking-at "[ \n\t\r]*}"))
          (insert ","))
      (unless (js-function-declaration-p)
        (if (looking-at "$") (insert ";")))))

  (defun snippet--function-name ()
    (if (js-function-declaration-p) "name" ""))
#+END_SRC

** EMMS
#+BEGIN_SRC emacs-lisp
  (require-package 'emms)

  (autoload 'emms-browser "emms-browser"
    "Launch or switch to the EMMS Browser." t)
  (autoload 'emms "emms-playlist-mode"
    "Switch to the current emms-playlist buffer." t)

  (eval-after-load "emms"
    `(progn
       (emms-devel)
       (emms-default-players)

       (if (require 'emms-info-libtag nil t)
           (add-to-list 'emms-info-functions 'emms-info-libtag
                        nil 'eq))
       (require 'emms-mark nil t)

       (require 'emms-history)
       (emms-history-load)

       ;; swap time and other track info
       (let ((new-global-mode-string nil))
         (while (and (not (memq (car global-mode-string)
                                '(emms-mode-line-string
                                  emms-playing-time-string)))
                     global-mode-string)
           (push (car global-mode-string) new-global-mode-string)
           (setq global-mode-string (cdr global-mode-string)))
         (setq global-mode-string
               (nconc (nreverse new-global-mode-string)
                      '(emms-playing-time-string
                        emms-mode-line-string))))
       (add-hook 'emms-player-started-hook 'emms-show)


       (defun my-emms-covers (dir type)
         "Choose album cover in DIR deppending on TYPE.
          Small cover should be less than 80000 bytes.
          Medium - less than 120000 bytes."
         (let* ((pics (directory-files-and-attributes
                       dir t "\\.\\(jpe?g\\|png\\|gif\\|bmp\\)$" t))
                (pic (car pics))
                (pic-size (nth 8 pic)))
           (let (temp)
             (cond
              ((eq type 'small)
               (while (setq temp (cadr pics))
                 (let ((temp-size (nth 8 temp)))
                   (if (< temp-size pic-size)
                       (setq pic temp
                             pic-size temp-size)))
                 (setq pics (cdr pics)))
               (if (<= (or pic-size 80001) 80000)
                   (car pic)))
              ((eq type 'medium)
               (if (and pic (setq temp (cadr pics)))
                   (progn
                     (setq pics (cdr pics))
                     (let ((temp-size (nth 8 temp)))
                       (let ((small temp)
                             (small-size temp-size))
                         (if (< pic-size small-size)
                             (setq small pic
                                   small-size pic-size
                                   pic temp
                                   pic-size temp-size))
                         (while (setq temp (cadr pics))
                           (setq temp-size (nth 8 temp))
                           (cond
                            ((< temp-size small-size)
                             (setq pic small
                                   pic-size small-size
                                   small temp
                                   small-size temp-size))
                            ((< temp-size pic-size)
                             (setq pic temp
                                   pic-size temp-size)))
                           (setq pics (cdr pics)))
                         (car (if (<= pic-size 120000) pic
                                small)))))
                 (car pic)))
              ((eq type 'large)
               (while (setq temp (cadr pics))
                 (let ((temp-size (nth 8 temp)))
                   (if (> temp-size pic-size)
                       (setq pic temp
                             pic-size temp-size)))
                 (setq pics (cdr pics)))
               (car pic))))))

       (setq emms-show-format "🎵 %s"
             emms-mode-line-format "%s"
             emms-playing-time-display-format "🎵 %s "
             emms-source-file-default-directory "~/Music"
             emms-browser-covers 'my-emms-covers)

       (require 'emms-player-mplayer)
       (define-emms-simple-player mplayer '(file url)
         (regexp-opt '(".ogg" ".mp3" ".wav" ".mpg" ".mpeg" ".wmv" ".wma"
                       ".mov" ".avi" ".divx" ".ogm" ".asf" ".mkv" "http://" "mms://"
                       ".rm" ".rmvb" ".mp4" ".flac" ".vob" ".m4a" ".flv" ".ogv" ".pls"))
         "mplayer" "-slave" "-quiet" "-really-quiet" "-fullscreen")

       (require 'emms-info-metaflac)
       (add-to-list 'emms-info-functions 'emms-info-metaflac nil 'eq)

       (global-set-key (kbd "C-c p") 'emms-pause)
       (global-set-key (kbd "C-c s") 'emms-stop)


       ;; track info ticker
       (defun string-shift-left (str &optional offset)
         "Shift STR content to the left OFFSET characters."
         (or offset (setq offset 1))
         (let ((str-len (length str)))
           (if (< offset str-len)
               (concat (substring-no-properties str offset)
                       (substring-no-properties str 0 offset))
             str)))

       (defun emms-tick-mode-line-description (offset)
         "Tick emms track description OFFSET characters."
         (setq emms-mode-line-string
               (string-shift-left emms-mode-line-string offset)))

       (defvar *my-emms-ticker* nil
         "Timer for current track info ticker.")

       (defun emms-track-ticker-start ()
         "Start ticking current TRACK info."
         (or *my-emms-ticker*
             (setq *my-emms-ticker*
                   (run-at-time t 2
                                'emms-tick-mode-line-description 5))))

       (defun emms-track-ticker-stop ()
         "Stop ticking current TRACK info."
         (when *my-emms-ticker*
           (cancel-timer *my-emms-ticker*)
           (setq *my-emms-ticker* nil)))


       (add-hook 'emms-player-started-hook 'emms-track-ticker-start)
       (add-hook 'emms-player-stopped-hook 'emms-track-ticker-stop)
       (add-hook 'emms-player-finished-hook 'emms-track-ticker-stop)
       (add-hook 'emms-player-paused-hook
                 (lambda () "Start/Stop track ticker."
                   (if *my-emms-ticker*
                       (emms-track-ticker-stop)
                     (emms-track-ticker-start))))))
#+END_SRC

Use `+` and `-` to change OS X system volume
#+BEGIN_SRC emacs-lisp
  (setq emms-volume-change-function
        (lambda (amount)
          (call-process "osascript" nil (current-buffer) nil
                        (format "-e set volume output volume (%s + %d)"
                                "(output volume of (get volume settings))"
                                amount))))
#+END_SRC
** Browse documentation using Dash
#+BEGIN_SRC emacs-lisp
  (require-package 'helm-dash)

  (defun add-dash-docsets (mode-hook &rest docsets)
    (add-hook mode-hook
              `(lambda () (setq-local helm-dash-docsets (quote ,docsets)))))

  (add-dash-docsets 'js2-mode-hook "JavaScript" "Lo-Dash")
  (add-dash-docsets 'clojure-mode-hook "Clojure")
  (add-dash-docsets 'emacs-lisp-mode-hook "Emacs Lisp")

  (setq helm-dash-browser-func 'eww)
  (global-set-key (kbd "C-c d") 'helm-dash-at-point)
#+END_SRC

** Jenkins integration
#+BEGIN_SRC emacs-lisp
  (require-package 'butler)
  (require 'butler)
#+END_SRC

** Guide-key
#+BEGIN_SRC emacs-lisp
  (add-to-list 'guide-key/guide-key-sequence "C-x v")
  (add-to-list 'guide-key/guide-key-sequence  "C-x 8")
  (add-to-list 'guide-key/guide-key-sequence  "C-c C-m")
  (setq guide-key/recursive-key-sequence-flag t)
  (setq guide-key/popup-window-position 'bottom)
  (setq guide-key/idle-delay 0.0)
#+END_SRC
** Deft
#+BEGIN_SRC emacs-lisp
  (require-package 'deft)

  (setq deft-directory "~/Dropbox/Documents"
        deft-extension "org"
        deft-text-mode 'org-mode
        deft-use-filename-as-title t)

  (global-set-key (kbd "<f9>") 'deft)
#+END_SRC

** AUCTeX
#+BEGIN_SRC emacs-lisp
  (require-package 'auctex)
#+END_SRC

** Nix
#+BEGIN_SRC emacs-lisp
(require-package 'nix-mode)
#+END_SRC

* Misc keybindings
Quick way to open a link
#+BEGIN_SRC emacs-lisp
  (global-set-key "\C-cb" 'org-open-at-point-global)
#+END_SRC

Switch to previous buffer
#+BEGIN_SRC emacs-lisp
  (global-set-key "\M-O" 'mode-line-other-buffer)
#+END_SRC

Prefer regex searches
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-s") 'isearch-forward-regexp)
  (global-set-key (kbd "C-r") 'isearch-backward-regexp)
  (global-set-key (kbd "C-M-s") 'isearch-forward)
  (global-set-key (kbd "C-M-r") 'isearch-backward)
#+END_SRC

Some useful bindings from emacs-starter-kit
#+BEGIN_SRC emacs-lisp
  (define-key global-map (kbd "C-+") 'text-scale-increase)
  (define-key global-map (kbd "C--") 'text-scale-decrease)
  (global-set-key (kbd "C-c y") 'bury-buffer)
  (global-set-key (kbd "C-c r") 'revert-buffer)

  (windmove-default-keybindings) ;; Shift+direction
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-x a r") 'align-regexp)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "<key-4660>") 'ignore)
#+END_SRC
* Misc tweaks
Use system Trash
#+BEGIN_SRC emacs-lisp
  (setq delete-by-moving-to-trash t)
#+END_SRC

No bell whatsoever please
#+BEGIN_SRC emacs-lisp
  (setq visual-bell nil)
  (setq ring-bell-function 'ignore)
#+END_SRC

Disable show-trailing-whitespace in some modes
#+BEGIN_SRC emacs-lisp
  (dolist (hook '(erc-mode-hook
                  term-mode-hook
                  eshell-mode-hook
                  nrepl-mode-hook
                  inferior-python-mode-hook
                  inferior-emacs-lisp-mode
                  helm-update-hook
                  slime-repl-mode-hook
                  mu4e-main-mode-hook
                  mu4e-headers-mode-hook
                  mu4e-view-mode-hook
                  jabber-roster-mode-hook
                  jabber-chat-mode-hook
                  jabber-browse-mode-hook))
    (add-hook hook (lambda () (setq show-trailing-whitespace nil))))
#+END_SRC

Wrap lines longer than 79 characters
#+BEGIN_SRC emacs-lisp
  (setq-default fill-column 79)
#+END_SRC

Set tmp dir to ~/.emacs.d/tmp
#+BEGIN_SRC emacs-lisp
  (setq temporary-file-directory (expand-file-name "tmp/" user-emacs-directory))

  (make-directory temporary-file-directory t)

  (setq backup-directory-alist
        `((".*" . ,temporary-file-directory)))
  (setq auto-save-file-name-transforms
        `((".*" ,temporary-file-directory t)))
#+END_SRC

Cleanup buffers before saving
#+BEGIN_SRC emacs-lisp
  (defun untabify-buffer ()
    (interactive)
    (untabify (point-min) (point-max)))

  (defun indent-buffer ()
    (interactive)
    (indent-region (point-min) (point-max)))

  (defun cleanup-buffer-safe ()
    "Perform a bunch of safe operations on the whitespace content of a buffer.
  Does not indent buffer, because it is used for a before-save-hook, and that
  might be bad."
    (interactive)
    (delete-trailing-whitespace)
    (set-buffer-file-coding-system 'utf-8))

  (defun cleanup-buffer ()
    "Perform a bunch of operations on the whitespace content of a buffer.
  Including indent-buffer, which should not be called automatically on save."
    (interactive)
    (cleanup-buffer-safe)
    (untabify-buffer)
    (indent-buffer))

  (add-hook 'before-save-hook 'cleanup-buffer-safe)

  (global-set-key (kbd "C-c n") 'cleanup-buffer)
  (global-set-key (kbd "C-c C-<return>") 'delete-blank-lines)
#+END_SRC

* Misc defuns
Utility function for creating a xmonad scratchpad
#+BEGIN_SRC emacs-lisp
  (defun osener-create-scratch-frame ()
    (select-frame
     (make-frame '((name . "emacs-scratch"))))
    (switch-to-buffer "*scratch*"))
#+END_SRC
